# Process this file with autoconf to produce a configure script.
AC_INIT(libreiser4/libreiser4.c)
AC_CANONICAL_HOST
AC_PREREQ(2.50)

AH_TEMPLATE([PACKAGE], [Define this to be the name of the package.])
AH_TEMPLATE([VERSION], [Define to the version of the package.])

AH_TEMPLATE([LIBREISER4_MAX_INTERFACE_VERSION], [])
AH_TEMPLATE([LIBREISER4_MIN_INTERFACE_VERSION], [])
AH_TEMPLATE([loff_t], [])
AH_TEMPLATE([ENABLE_COMPACT], [])
AH_TEMPLATE([ENABLE_MONOLITHIC], [])
AH_TEMPLATE([ENABLE_DEBUG], [])
AH_TEMPLATE([HAVE_LIBUUID], [])
AH_TEMPLATE([HAVE_LIBREADLINE], [])

LIBREISER4_MAJOR_VERSION=0
LIBREISER4_MINOR_VERSION=3
LIBREISER4_MICRO_VERSION=3

LIBREISER4_INTERFACE_AGE=0
LIBREISER4_BINARY_AGE=0
LIBREISER4_VERSION=$LIBREISER4_MAJOR_VERSION.$LIBREISER4_MINOR_VERSION.$LIBREISER4_MICRO_VERSION

LT_RELEASE=$LIBREISER4_MAJOR_VERSION.$LIBREISER4_MINOR_VERSION

LT_CURRENT=`expr $LIBREISER4_MICRO_VERSION - $LIBREISER4_INTERFACE_AGE`

LT_REVISION=$LIBREISER4_INTERFACE_AGE

LT_AGE=`expr $LIBREISER4_BINARY_AGE - $LIBREISER4_INTERFACE_AGE`

AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

PACKAGE=reiser4progs
VERSION=$LIBREISER4_VERSION

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AM_CONFIG_HEADER(config.h)

AC_DEFINE_UNQUOTED(LIBREISER4_MAX_INTERFACE_VERSION, $LIBREISER4_BINARY_AGE)
AC_DEFINE_UNQUOTED(LIBREISER4_MIN_INTERFACE_VERSION, $LIBREISER4_INTERFACE_AGE)

AC_ARG_ENABLE(debug, 
    [  --enable-debug          compile in assertions [default=yes]], , 
	enable_debug=yes
)

AC_ARG_WITH(uuid,
    [  --with-uuid             support uuid generating and checking], ,
        with_uuid=yes
)

dnl Check for libuuid
if test "$with_uuid" = yes; then
    OLD_LIBS="$LIBS"
    LIBS=""
    AC_CHECK_LIB(uuid, uuid_generate, ,
	AC_MSG_WARN(
libuuid could not be found which is required for the --with-uuid
	)
	with_uuid=no
    )
    UUID_LIBS="$LIBS"
    LIBS="$OLD_LIBS"
fi

AC_SUBST(UUID_LIBS)

AC_ARG_WITH(readline,
    [  --with-readline         support fancy command line editing], ,
        with_readline=yes
)

dnl Check for termcap
if test "$with_readline" = yes; then
	OLD_LIBS="$LIBS"
	LIBS=""
	AC_SEARCH_LIBS(tgetent, ncurses curses termcap termlib,
		PROGS_LIBS="$PROGS_LIBS $LIBS",
		AC_MSG_WARN(
termcap could not be found which is required for the
--with-readline option (which is enabled by default).
		)
		with_readline=no
	)
	LIBS="$OLD_LIBS"
fi

dnl Check for readline
if test "$with_readline" = yes; then
    OLD_LIBS="$LIBS"
    LIBS=""
    AC_CHECK_LIB(readline, readline, ,
	    AC_MSG_WARN(
GNU Readline could not be found which is required for the --with-readline
	    )
	    with_readline=no
    )
    PROGS_LIBS="$PROGS_LIBS $LIBS"
    LIBS="$OLD_LIBS"
fi

AC_SUBST(PROGS_LIBS)

AM_ENABLE_SHARED

AC_ARG_ENABLE(compact,
    [  --enable-compact        build in compact maner (without libc) [default=no]], ,
        enable_compact=no
)
if test x$enable_compact = xyes; then
    enable_debug=no;
    AC_DEFINE(ENABLE_COMPACT)
fi

AC_ARG_ENABLE(monolithic,
    [  --enable-monolithic     build in monolithic maner (with builtin plugins) [default=yes]], ,
        enable_monolithic=yes
)

if test x$enable_monolithic = xyes; then
    AC_DEFINE(ENABLE_MONOLITHIC)
fi

if test x$enable_debug = xyes; then
    AC_DEFINE(ENABLE_DEBUG)
fi

AC_ARG_ENABLE(Werror,
    [  --enable-Werror         build with gcc -Werror [default=yes]], ,
	enable_Werror=yes
)

AC_ARG_WITH(plugin-dir, 
    [  --with-plugin-dir=PATH  directory plugins will lie in]
)
if test x$with_plugin_dir = x; then
    with_plugin_dir="$libdir/reiser4"
fi

plugindir=$with_plugin_dir
AC_SUBST(plugindir)

dnl make libc threadsafe (not required for us, but useful other users of
dnl libreiser4)
if test x$enable_compact = xno; then
    CFLAGS="$CFLAGS -D_REENTRANT"
fi

# Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_AWK

AC_SYS_LARGEFILE

if test -z "${ac_cv_sys_file_offset_bits}"; then
	AC_MSG_WARN(
Can't detect right _FILE_OFFSET_BITS. Will be forced to 64bit.)
	ac_cv_sys_file_offset_bits=64
fi

CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=${ac_cv_sys_file_offset_bits}"
AC_CHECK_SIZEOF(off_t, 64, [
	#include <stdio.h>
	#include <sys/types.h>
	#include <unistd.h>
])

AM_PROG_LIBTOOL

if test "$with_uuid" = yes; then
    AC_CHECK_HEADERS(uuid/uuid.h, ,
	AC_MSG_WARN(
The headers for libuuid could not be found which
are required for the --with-uuid option.
	)
    )
fi

if test "$with_readline" = yes; then
    AC_CHECK_HEADERS(readline/readline.h readline/history.h, ,
	AC_MSG_WARN(
The headers for GNU Readline could not be found which
are required for the --with-readline option.
	)
    )
fi

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h fcntl.h mntent.h stdint.h stdlib.h string.h sys/ioctl.h sys/mount.h sys/vfs.h unistd.h])

dnl Checks for typedefs, structures and compiler characteristics.
AC_C_BIGENDIAN

dnl Checks for library functions.
if test "$with_readline" = yes; then
    OLD_LIBS="$LIBS"
    LIBS="$LIBS $PROGS_LIBS"
    AC_CHECK_FUNCS(rl_completion_matches)
    LIBS="$OLD_LIBS"
fi

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_CHECK_MEMBERS([struct stat.st_rdev])

# Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_STAT
AC_CHECK_FUNCS([statfs getmntent hasmntopt memset strerror strtol])

dnl Check for libdl, if we are doing dynamic loading
LIBREISER4_LIBS=""
if test x$enable_compact = xno && test x$enable_monolithic = xno; then
    OLD_LIBS="$LIBS"
    LIBS=""
    AC_CHECK_LIB(dl, dlopen, ,
	AC_MSG_ERROR(libdl not found!)
	exit
    )
    LIBREISER4_LIBS="$LIBREISER4_LIBS $LIBS"
    LIBS="$OLD_LIBS"
fi
AC_SUBST(LIBREISER4_LIBS)

if test x$enable_debug = xyes; then
    CFLAGS="$CFLAGS -g"
fi

if test x$enable_compact = xyes; then
    CFLAGS="$CFLAGS -O9 -fno-builtin"
fi

CFLAGS="$CFLAGS -W -Wall -Wno-unused"
if test x$enable_Werror = xyes; then
    CFLAGS="$CFLAGS -Werror"
fi

CFLAGS="$CFLAGS -DPLUGIN_DIR=\\\"$with_plugin_dir\\\""

AM_CONDITIONAL(ENABLE_MONOLITHIC, test x$enable_monolithic = xyes)

PLUGIN_LDFLAGS=""
if test x$enable_monolithic = xno; then
    PLUGIN_LDFLAGS="-module -avoid-version"
fi
AC_SUBST(PLUGIN_LDFLAGS)

AC_SUBST(PROGS_LDFLAGS)

AM_CONDITIONAL(ENABLE_COMPACT, test x$enable_compact = xyes)

AC_OUTPUT([
    Makefile
    include/Makefile
    include/aal/Makefile
    include/aux/Makefile
    include/reiser4/Makefile
    include/repair/Makefile
    libaal/Makefile
    libaux/Makefile
    libreiser4/Makefile
    librepair/Makefile
    plugin/Makefile
    plugin/format/Makefile
    plugin/format/format40/Makefile
    plugin/alloc/Makefile
    plugin/alloc/alloc40/Makefile
    plugin/journal/Makefile
    plugin/journal/journal40/Makefile
    plugin/oid/Makefile
    plugin/oid/oid40/Makefile
    plugin/node/Makefile
    plugin/node/node40/Makefile
    plugin/key/Makefile
    plugin/key/key40/Makefile
    plugin/item/Makefile
    plugin/item/nodeptr40/Makefile
    plugin/item/stat40/Makefile
    plugin/item/direntry40/Makefile
    plugin/item/tail40/Makefile
    plugin/item/extent40/Makefile
    plugin/file/Makefile
    plugin/file/file40/Makefile
    plugin/file/dir40/Makefile
    plugin/file/reg40/Makefile
    plugin/file/symlink40/Makefile
    plugin/hash/Makefile
    plugin/hash/r5_hash/Makefile
    plugin/hash/tea_hash/Makefile
    plugin/hash/rupasov_hash/Makefile
    plugin/hash/fnv1_hash/Makefile
    plugin/sdext/Makefile
    plugin/sdext/sdext_unix/Makefile
    plugin/sdext/sdext_lw/Makefile
    plugin/sdext/sdext_lt/Makefile
    plugin/sdext/sdext_symlink/Makefile
    demos/Makefile
    progs/Makefile
    progs/include/Makefile
    progs/include/misc/Makefile
    progs/libmisc/Makefile
    progs/mkfs/Makefile
    progs/fsck/Makefile
    progs/debugfs/Makefile
    debug/Makefile
    doc/Makefile
    reiser4progs.spec
])

echo
echo Type \'make\' to compile reiser4progs

