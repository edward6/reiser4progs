# Process this file with autoconf to produce a configure script.
AC_INIT(libreiser4/libreiser4.c)
AC_CANONICAL_HOST
AC_PREREQ(2.50)

AH_TEMPLATE([PACKAGE], [Define this to be the name of the package.])
AH_TEMPLATE([VERSION], [Define to the version of the package.])

AH_TEMPLATE([LIBREISER4_MAX_INTERFACE_VERSION], [Define to the max interface version.])
AH_TEMPLATE([LIBREISER4_MIN_INTERFACE_VERSION], [Define to the min interface version.])
AH_TEMPLATE([loff_t], [Define to loff_t])
AH_TEMPLATE([ENABLE_DEBUG], [Define for enable debug info.])
AH_TEMPLATE([ENABLE_MONOLITHIC], [Define for enable monolotic build.])
AH_TEMPLATE([ENABLE_MEMORY_MANAGER], [Define for enable simple memory manager in libaal.])
AH_TEMPLATE([ENABLE_COLLISIONS_HANDLING], [Define for enable key collision workaround code.])
AH_TEMPLATE([ENABLE_PLUGINS_CHECK], [Define for enable plugins check code.])
AH_TEMPLATE([HAVE_LIBUUID], [Define for enable libuuid using.])
AH_TEMPLATE([HAVE_LIBREADLINE], [Define for enable libreadline using.])

LIBREISER4_MAJOR_VERSION=0
LIBREISER4_MINOR_VERSION=3
LIBREISER4_MICRO_VERSION=9

LIBREISER4_INTERFACE_AGE=0
LIBREISER4_BINARY_AGE=0
LIBREISER4_VERSION=$LIBREISER4_MAJOR_VERSION.$LIBREISER4_MINOR_VERSION.$LIBREISER4_MICRO_VERSION

LT_RELEASE=$LIBREISER4_MAJOR_VERSION.$LIBREISER4_MINOR_VERSION

LT_CURRENT=`expr $LIBREISER4_MICRO_VERSION - $LIBREISER4_INTERFACE_AGE`

LT_REVISION=$LIBREISER4_INTERFACE_AGE

LT_AGE=`expr $LIBREISER4_BINARY_AGE - $LIBREISER4_INTERFACE_AGE`

AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

PACKAGE=reiser4progs
VERSION=$LIBREISER4_VERSION

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AM_CONFIG_HEADER(config.h)

AC_DEFINE_UNQUOTED(LIBREISER4_MAX_INTERFACE_VERSION, $LIBREISER4_BINARY_AGE)
AC_DEFINE_UNQUOTED(LIBREISER4_MIN_INTERFACE_VERSION, $LIBREISER4_INTERFACE_AGE)

AC_ARG_WITH(uuid,
    [  --with-uuid             support uuid generating and checking], ,
        with_uuid=yes
)

dnl Check for libuuid
if test "$with_uuid" = yes; then
    OLD_LIBS="$LIBS"
    LIBS=""
    AC_CHECK_LIB(uuid, uuid_generate, ,
	AC_MSG_WARN(
libuuid could not be found which is required for the --with-uuid
	)
	with_uuid=no
    )
    UUID_LIBS="$LIBS"
    LIBS="$OLD_LIBS"
fi

AC_SUBST(UUID_LIBS)

AC_ARG_WITH(readline,
    [  --with-readline         support fancy command line editing], ,
        with_readline=yes
)

dnl Check for termcap
if test "$with_readline" = yes; then
	OLD_LIBS="$LIBS"
	LIBS=""
	AC_SEARCH_LIBS(tgetent, ncurses curses termcap termlib,
		PROGS_LIBS="$PROGS_LIBS $LIBS",
		AC_MSG_WARN(
termcap could not be found which is required for the
--with-readline option (which is enabled by default).
		)
		with_readline=no
	)
	LIBS="$OLD_LIBS"
fi

dnl Check for readline
if test "$with_readline" = yes; then
    OLD_LIBS="$LIBS"
    LIBS=""
    AC_CHECK_LIB(readline, readline, ,
	    AC_MSG_WARN(
GNU Readline could not be found which is required for the --with-readline
	    )
	    with_readline=no
    )
    PROGS_LIBS="$PROGS_LIBS $LIBS"
    LIBS="$OLD_LIBS"
fi

AC_SUBST(PROGS_LIBS)

AM_ENABLE_SHARED

AC_ARG_ENABLE(debug, 
    [  --disable-debug                disable debug information in binaries [default=on]], , 
	enable_debug=yes
)

AC_ARG_ENABLE(stand_alone,
    [  --enable-stand-alone           enable build stand alone library [default=off]], ,
        enable_stand_alone=no
)

AC_ARG_ENABLE(monolithic,
    [  --disable-monolithic           disable including all plugins in [default=on]], ,
        enable_monolithic=yes
)

AC_ARG_ENABLE(memory_manager,
    [  --enable-memory-manager        enable memory manager in libaal [default=off]], ,
        enable_memory_manager=no
)

AC_ARG_ENABLE(collisions_handling,
    [  --disable-collisions-handling  disable key collision handling [default=on]], ,
        enable_collisions_handling=yes
)

AC_ARG_ENABLE(plugins_check,
    [  --disable-plugins-check        disable plugins check in plugin factory [default=on]], ,
        enable_plugins_check=yes
)

AC_ARG_ENABLE(r5_hash,
    [  --disable-r5-hash              disable r5 hash plugin [default=on]], ,
        enable_r5_hash=yes
)

AC_ARG_ENABLE(fnv1_hash,
    [  --disable-fnv1-hash            disable fnv1 hash plugin [default=on]], ,
        enable_fnv1_hash=yes
)

AC_ARG_ENABLE(rupasov_hash,
    [  --disable-rupasov-hash         disable rupasov hash plugin [default=on]], ,
        enable_rupasov_hash=yes
)

AC_ARG_ENABLE(tea_hash,
    [  --disable-tea-hash             disable tea hash plugin [default=on]], ,
        enable_tea_hash=yes
)

AC_ARG_ENABLE(Werror,
    [  --disable-Werror               disable build with gcc -Werror [default=on]], ,
	enable_Werror=yes
)

AC_ARG_WITH(plugin-dir, 
    [  --with-plugin-dir=PATH         directory stand alone plugins will lie in]
)
if test x$with_plugin_dir = x; then
    with_plugin_dir="$libdir/reiser4"
fi

plugindir=$with_plugin_dir
AC_SUBST(plugindir)

# Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_AWK

CFLAGS=""
ALONE_CFLAGS=""
GENERIC_CFLAGS=""

dnl make libc threadsafe (not required for us for awhile, but useful other users of
dnl ibreiser4)
GENERIC_CFLAGS="$GENERIC_CFLAGS -D_REENTRANT"

AC_SYS_LARGEFILE

if test -z "${ac_cv_sys_file_offset_bits}"; then
	AC_MSG_WARN(
Can't detect right _FILE_OFFSET_BITS. Will be forced to 64bit.)
	ac_cv_sys_file_offset_bits=64
fi

GENERIC_CFLAGS="$GENERIC_CFLAGS -D_FILE_OFFSET_BITS=${ac_cv_sys_file_offset_bits}"
AC_CHECK_SIZEOF(off_t, 64, [
	#include <stdio.h>
	#include <sys/types.h>
	#include <unistd.h>
])

AM_PROG_LIBTOOL

if test "$with_uuid" = yes; then
    AC_CHECK_HEADERS(uuid/uuid.h, ,
	AC_MSG_WARN(
The headers for libuuid could not be found which
are required for the --with-uuid option.
	)
    )
fi

if test "$with_readline" = yes; then
    AC_CHECK_HEADERS(readline/readline.h readline/history.h, ,
	AC_MSG_WARN(
The headers for GNU Readline could not be found which
are required for the --with-readline option.
	)
    )
fi

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([printf.h errno.h fcntl.h mntent.h stdint.h stdlib.h string.h sys/ioctl.h sys/mount.h sys/vfs.h unistd.h])

dnl Checks for typedefs, structures and compiler characteristics.
AC_C_BIGENDIAN

dnl Checks for library functions.
if test "$with_readline" = yes; then
    OLD_LIBS="$LIBS"
    LIBS="$LIBS $PROGS_LIBS"
    AC_CHECK_FUNCS(rl_completion_matches)
    LIBS="$OLD_LIBS"
fi

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_CHECK_MEMBERS([struct stat.st_rdev])

# Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_STAT
AC_CHECK_FUNCS([register_printf_function statfs getmntent hasmntopt memset strerror strtol time])

dnl Check for libdl, if we are doing dynamic loading
LIBREISER4_LIBS=""
if test x$enable_monolithic = xno; then
    OLD_LIBS="$LIBS"
    LIBS=""
    AC_CHECK_LIB(dl, dlopen, ,
	AC_MSG_ERROR(libdl not found!)
	exit
    )
    LIBREISER4_LIBS="$LIBREISER4_LIBS $LIBS"
    LIBS="$OLD_LIBS"
fi
AC_SUBST(LIBREISER4_LIBS)

if test x$enable_stand_alone = xyes; then
    ALONE_CFLAGS="$ALONE_CFLAGS -DENABLE_STAND_ALONE -DENABLE_MONOLITHIC -fno-builtin"
fi

if test x$enable_debug = xyes; then
    GENERIC_CFLAGS="$GENERIC_CFLAGS -O0 -g"
    ALONE_CFLAGS="$ALONE_CFLAGS -O0 -g"
else
    GENERIC_CFLAGS="$GENERIC_CFLAGS -O2"
    ALONE_CFLAGS="$ALONE_CFLAGS -O9"
fi

CFLAGS="$CFLAGS -W -Wall -Wno-unused"
if test x$enable_Werror = xyes; then
    CFLAGS="$CFLAGS -Werror"
fi

CFLAGS="$CFLAGS -DPLUGIN_DIR=\\\"$with_plugin_dir\\\""

AM_CONDITIONAL(ENABLE_MONOLITHIC, test x$enable_monolithic = xyes)

if test x$enable_monolithic = xyes; then
    AC_DEFINE(ENABLE_MONOLITHIC)
fi

if test x$enable_memory_manager = xyes; then
    AC_DEFINE(ENABLE_MEMORY_MANAGER)
fi

if test x$enable_collisions_handling = xyes; then
    AC_DEFINE(ENABLE_COLLISIONS_HANDLING)
fi

if test x$enable_plugins_check = xyes; then
    AC_DEFINE(ENABLE_PLUGINS_CHECK)
fi

AM_CONDITIONAL(ENABLE_R5_HASH, test x$enable_r5_hash = xyes)
AM_CONDITIONAL(ENABLE_FNV1_HASH, test x$enable_fnv1_hash = xyes)
AM_CONDITIONAL(ENABLE_RUPASOV_HASH, test x$enable_rupasov_hash = xyes)
AM_CONDITIONAL(ENABLE_TEA_HASH, test x$enable_tea_hash = xyes)

PLUGIN_LDFLAGS=""
if test x$enable_monolithic = xno; then
    PLUGIN_LDFLAGS="-module -avoid-version"
fi
AC_SUBST(PLUGIN_LDFLAGS)

AC_SUBST(PROGS_LDFLAGS)

AM_CONDITIONAL(ENABLE_DEBUG, test x$enable_debug = xyes)
AM_CONDITIONAL(ENABLE_STAND_ALONE, test x$enable_alone = xyes)

if test x$enable_debug = xyes; then
    AC_DEFINE(ENABLE_DEBUG)
fi

AC_SUBST(ALONE_CFLAGS)
AC_SUBST(GENERIC_CFLAGS)

AC_OUTPUT([
    Makefile
    include/Makefile
    include/aal/Makefile
    include/aux/Makefile
    include/reiser4/Makefile
    include/repair/Makefile
    libaal/Makefile
    libaux/Makefile
    libreiser4/Makefile
    librepair/Makefile
    plugin/Makefile
    plugin/format/Makefile
    plugin/format/format40/Makefile
    plugin/alloc/Makefile
    plugin/alloc/alloc40/Makefile
    plugin/journal/Makefile
    plugin/journal/journal40/Makefile
    plugin/oid/Makefile
    plugin/oid/oid40/Makefile
    plugin/node/Makefile
    plugin/node/node40/Makefile
    plugin/key/Makefile
    plugin/key/key40/Makefile
    plugin/item/Makefile
    plugin/item/nodeptr40/Makefile
    plugin/item/stat40/Makefile
    plugin/item/direntry40/Makefile
    plugin/item/tail40/Makefile
    plugin/item/extent40/Makefile
    plugin/object/Makefile
    plugin/object/obj40/Makefile
    plugin/object/dir40/Makefile
    plugin/object/reg40/Makefile
    plugin/object/sym40/Makefile
    plugin/hash/Makefile
    plugin/hash/r5_hash/Makefile
    plugin/hash/tea_hash/Makefile
    plugin/hash/rupasov_hash/Makefile
    plugin/hash/fnv1_hash/Makefile
    plugin/sdext/Makefile
    plugin/sdext/sdext_unix/Makefile
    plugin/sdext/sdext_lw/Makefile
    plugin/sdext/sdext_lt/Makefile
    plugin/sdext/sdext_symlink/Makefile
    demos/Makefile
    progs/Makefile
    progs/include/Makefile
    progs/include/misc/Makefile
    progs/libmisc/Makefile
    progs/mkfs/Makefile
    progs/fsck/Makefile
    progs/debugfs/Makefile
    debug/Makefile
    doc/Makefile
])

echo
echo Type \'make\' to compile reiser4progs

